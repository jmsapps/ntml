import
  dom,
  macros,
  strutils,
  hashes

import
  mount,
  overloads,
  routing,
  shims,
  signals,
  styled

import
  types


macro defineHtmlElement*(tagNameLit: static[string]; args: varargs[untyped]): untyped =
  var tagName: string = tagNameLit
  let node: NimNode = genSym(nskLet, "node")
  let statements: NimNode = newTree(nnkStmtListExpr)

  case tagName
  of "d": tagName = "div"
  of "obj": tagName = "object"
  of "tmpl": tagName = "template"
  of "v": tagName = "var"
  else: discard

  var children: seq[NimNode] = @[]
  var eventNames: seq[string] = @[]
  var eventHandlers: seq[NimNode] = @[]
  var attrSetters: seq[NimNode] = @[]

  proc pushChild(node: NimNode) {.compileTime.} =
    children.add(node)

  proc lowerMountAttributes(keyRaw: string, value: NimNode) {.compileTime.} =
    var key: string = keyRaw

    if key == "className":
      key = "class"

    let keyLowered: string = key.toLowerAscii()
    let kLit: NimNode = newLit(key)

    if keyLowered.len >= 3 and keyLowered.startsWith("on"):
      let event: string = keyLowered[2..^1]
      eventNames.add(event)
      eventHandlers.add(value)

      return

    if keyLowered == "value":
      attrSetters.add(newCall(ident"bindValue", node, value))

      return

    elif keyLowered == "checked":
      attrSetters.add(newCall(ident"bindChecked", node, value))

      return

    elif keyLowered == "css":
      # compute a stable class (compile-time hash over the literal)
      if value.kind in {nnkStrLit, nnkTripleStrLit}:
        let cssStr: string = value.strVal

        # simple stable hash for a compact suffix:
        proc fnv1a32(s: string): uint32 {.compileTime.} =
          var h: uint32 = 2166136261'u32
          for c in s:
            h = (h xor uint32(ord(c))) * 16777619'u32

          h

        let hashStr: string = $fnv1a32(cssStr)
        let suffix: string = (if hashStr.len >= 6: hashStr[^6..^1] else: hashStr)
        let clsName: string = "s-" & suffix

        attrSetters.add(newCall(ident"injectCssOnce", newLit(clsName), newLit(cssStr)))
        attrSetters.add(newCall(ident"markStyledClass", node, newLit(clsName)))
        # force one class write so unionWithStyled runs at least once
        attrSetters.add(newCall(ident"setStringAttr", node, newLit("class"), newLit("")))

        return

      else:
        # non-literal css: fall back to runtime path (hash string at runtime)
        let tmpCss: NimNode = genSym(nskLet, "css")
        let tmpCls: NimNode = genSym(nskLet, "cls")
        attrSetters.add(newLetStmt(tmpCss, value))

        # simple runtime hash (optionally mirror FNV in JS via emit)
        attrSetters.add(quote do:
          let `tmpCls` = "s-" & $hash(`tmpCss`)
          injectCssOnce(`tmpCls`, `tmpCss`)
          markStyledClass(`node`, `tmpCls`)
          setStringAttr(`node`, "class", "")
        )

        return

    elif keyLowered == "cssvars" or keyLowered == "stylevars":
      attrSetters.add(newCall(ident"applyStyleVars", node, value))
      return

    if value.kind == nnkIfExpr or value.kind == nnkIfStmt:
      var cond, thenExpr, elseExpr: NimNode
      let head: NimNode = value[0]

      cond = head[0]
      thenExpr = (if head[1].kind == nnkStmtList and head[1].len > 0: head[1][^1] else: head[1])
      elseExpr = newLit("")

      for br in value[1..^1]:
        if br.kind in {nnkElse, nnkElseExpr}:
          let body = br[0]
          elseExpr = (if body.kind == nnkStmtList and body.len > 0: body[^1] else: body)

      attrSetters.add(newCall(ident"mountAttrIf", node, kLit, cond, thenExpr, elseExpr))

      return

    if value.kind == nnkCaseStmt:
      let disc: NimNode = value[0]
      let sel: NimNode  = ident"caseDisc"
      var caseNode: NimNode = newTree(nnkCaseStmt, sel)

      for br in value[1..^1]:
        case br.kind
        of nnkOfBranch:
          var branch: NimNode = newTree(nnkOfBranch)

          for lit in br[0..^2]:
            branch.add(lit)

          let body: NimNode = br[^1]
          let expr: NimNode = (if body.kind == nnkStmtList and body.len > 0: body[^1] else: body)

          branch.add(expr)
          caseNode.add(branch)

        of nnkElse:
          let body: NimNode = br[0]
          let expr: NimNode = (if body.kind == nnkStmtList and body.len > 0: body[^1] else: body)
          caseNode.add(newTree(nnkElse, expr))

        else: discard

      attrSetters.add(newCall(ident"mountAttrCase", node, kLit, disc, caseNode))

      return

    attrSetters.add(newCall(ident"mountAttr", node, kLit, value))

  proc extractKeyAttribute(body: NimNode; keyName: string = "key"): tuple[clean: NimNode, keyExpr: NimNode] {.compileTime.} =
    var keyCaptured: NimNode

    proc strip(node: NimNode): NimNode {.compileTime.} =
      let nodeKind = node.kind
      case nodeKind
      of nnkStmtList, nnkStmtListExpr, nnkBlockStmt:
        result = newTree(nodeKind)
        for child in node:
          result.add(strip(child))

      of nnkCall, nnkCommand, nnkCallStrLit:
        result = newTree(nodeKind)
        if node.len > 0:
          result.add(strip(node[0]))
        for i in 1 ..< node.len:
          let arg = node[i]
          var keep = true
          if keyCaptured.isNil:
            case arg.kind
            of nnkExprEqExpr:
              let lhs = arg[0]
              if lhs.kind == nnkIdent and lhs.strVal.toLowerAscii() == keyName:
                keyCaptured = arg[1]
                keep = false

            of nnkInfix:
              if arg.len == 3 and arg[0].kind == nnkIdent and arg[0].strVal == "=":
                let lhs = arg[1]
                if lhs.kind == nnkIdent and lhs.strVal.toLowerAscii() == keyName:
                  keyCaptured = arg[2]
                  keep = false

            else:
              discard
          if keep:
            result.add(strip(arg))

      else:
        result = copyNimTree(node)

    result.clean = strip(body)
    result.keyExpr = keyCaptured


  proc lowerMountChildren(parent, node: NimNode): NimNode {.compileTime.} =
    proc emptyFragmentExpr(): NimNode {.compileTime.} =
      let cont = genSym(nskLet, "cont")
      let lambdaProc = newProc(
        name = newEmptyNode(),
        params = @[ident"Node"],
        procType = nnkLambda,
        body = newStmtList(
          newLetStmt(cont, newCall(ident"jsCreateFragment")),
          cont
        )
      )
      let builderSym = genSym(nskLet, "builder")
      newTree(nnkStmtListExpr,
        newLetStmt(builderSym, lambdaProc),
        builderSym
      )

    proc toExpr(body: NimNode): NimNode {.compileTime.} =
      let cont: NimNode = genSym(nskLet, "cont")
      var builderBody: NimNode = newStmtList(
        newLetStmt(cont, newCall(ident"jsCreateFragment"))
      )

      if body.kind in {nnkStmtList, nnkStmtListExpr}:
        for stmt in body:
          builderBody.add(lowerMountChildren(cont, stmt))
      else:
        builderBody.add(lowerMountChildren(cont, body))

      builderBody.add(cont)

      let lambdaProc = newProc(
        name = newEmptyNode(),
        params = @[ident"Node"],
        procType = nnkLambda,
        body = builderBody
      )
      let builderSym = genSym(nskLet, "builder")
      result = newTree(nnkStmtListExpr,
        newLetStmt(builderSym, lambdaProc),
        builderSym
      )

    case node.kind
    of nnkStmtList, nnkStmtListExpr, nnkBlockStmt:
      result = newTree(nnkStmtList)
      for it in node:
        result.add(lowerMountChildren(parent, it))

    of nnkIfStmt:
      var conds: seq[NimNode] = @[]
      var bodies: seq[NimNode] = @[]
      var elseBody: NimNode

      for br in node:
        case br.kind
        of nnkElifBranch:
          conds.add(br[0])
          bodies.add(toExpr(br[1]))

        of nnkElse:
          elseBody = toExpr(br[0])

        else:
          discard

      let anyPrior: NimNode = genSym(nskVar, "anyPrior")
      var stmts: NimNode = newStmtList(
        newVarStmt(anyPrior, newCall(ident"signal", newLit(false)))
      )

      for i in 0 ..< conds.len:
        let ciSig: NimNode = conds[i]

        let gated: NimNode =
          if i == 0:
            ciSig
          else:
            newCall(ident"and", newCall(ident"not", anyPrior), ciSig)

        stmts.add(newCall(
          ident"mountChildIf",
          parent,
          gated,
          bodies[i],
          emptyFragmentExpr()
        ))

        stmts.add(newAssignment(anyPrior, newCall(ident"or", anyPrior, ciSig)))

      if not elseBody.isNil:
        let elseCond: NimNode = newCall(ident"not", anyPrior)
        stmts.add(newCall(
          ident"mountChildIf",
          parent,
          elseCond,
          elseBody,
          emptyFragmentExpr()
        ))

      result = stmts

    of nnkCaseStmt:
      let disc: NimNode = node[0]
      let sel: NimNode = ident"caseDisc"

      let caseNode = newTree(nnkCaseStmt, sel)

      for br in node[1..^1]:
        case br.kind
        of nnkOfBranch:
          var branch: NimNode = newTree(nnkOfBranch)

          for lit in br[0..^2]:
            branch.add(lit)
          branch.add(toExpr(br[^1]))
          caseNode.add(branch)

        of nnkElse:
          caseNode.add(newTree(nnkElse, toExpr(br[0])))

        else: discard

      result = newCall(ident"mountChildCase", parent, disc, caseNode)

    of nnkForStmt:
      # ForStmt: <name(s)> ... <iterExpr> <StmtList>
      var bodyIdx = -1

      for i in countdown(node.len - 1, 0):
        if node[i].kind == nnkStmtList:
          bodyIdx = i
          break

      let bodyNode: NimNode = node[bodyIdx]
      let extracted = extractKeyAttribute(bodyNode)
      let bodyForRender: NimNode = extracted.clean
      var iterExpr: NimNode = node[bodyIdx - 1]

      var names: seq[NimNode] = @[]
      for i in 0 ..< bodyIdx - 1:
        names.add(node[i])

      let renderFn: NimNode = genSym(nskProc, "render")
      let renderItSym: NimNode = genSym(nskParam, "it")
      let keyItSym: NimNode = if extracted.keyExpr.isNil: renderItSym else: genSym(nskParam, "it")
      let frag: NimNode = genSym(nskLet,  "frag")

      proc makeBindDefs(itSym: NimNode): NimNode {.compileTime.} =
        if names.len == 1:
          newTree(nnkIdentDefs, names[0], newEmptyNode(), itSym)
        else:
          let tupleDef = newTree(nnkVarTuple)
          for nm in names:
            tupleDef.add(nm)
          tupleDef.add(newEmptyNode())
          tupleDef.add(itSym)
          tupleDef

      # build binding defs
      var bindDefsRender: NimNode
      if names.len == 1:
        bindDefsRender = makeBindDefs(renderItSym)
      else:
        # enumerate: convert iterable to seq[(int,T)] or Signal[seq[(int,T)]]
        iterExpr = newCall(ident"toIndexSeq", iterExpr)
        bindDefsRender = makeBindDefs(renderItSym)

      let bindSectionRender = newTree(nnkLetSection, bindDefsRender)

      let renderProc: NimNode = newProc(
        renderFn,
        params = [ident"Node", newIdentDefs(renderItSym, ident"auto")],
        body = newTree(nnkStmtList,
          newLetStmt(frag, newCall(ident"jsCreateFragment")),
          copyNimTree(bindSectionRender),
          lowerMountChildren(frag, bodyForRender),
          frag
        )
      )

      if extracted.keyExpr.isNil:
        result = newTree(nnkStmtList,
          renderProc,
          newCall(ident"mountChildFor", parent, newCall(ident"guardSeq", iterExpr), renderFn)
        )
      else:
        let bindDefsKey = makeBindDefs(keyItSym)
        let bindSectionKey = newTree(nnkLetSection, bindDefsKey)
        let keyFn: NimNode = genSym(nskProc, "key")
        let keyProc: NimNode = newProc(
          keyFn,
          params = [ident"string", newIdentDefs(keyItSym, ident"auto")],
          body = newTree(nnkStmtList,
            copyNimTree(bindSectionKey),
            newCall(ident"$", copyNimTree(extracted.keyExpr))
          )
        )

        result = newTree(nnkStmtList,
          renderProc,
          keyProc,
          newCall(ident"mountChildForKeyed", parent, newCall(ident"guardSeq", iterExpr), keyFn, renderFn)
        )

    of nnkWhileStmt:
      let loop: NimNode = copy(node)
      loop[^1] = lowerMountChildren(parent, node[^1])
      result = loop

    of nnkLetSection, nnkVarSection, nnkConstSection, nnkAsgn, nnkDiscardStmt:
      result = node

    else:
      result = newCall(ident"mountChild", parent, node)

  for a in args:
    case a.kind
    of nnkStmtList, nnkStmtListExpr:
      for it in a:
        pushChild(it)

    of nnkExprEqExpr:
      lowerMountAttributes($a[0], a[1])

    of nnkInfix:
      if a[0].kind == nnkIdent and $a[0] == "=":
        lowerMountAttributes($a[1], a[2])

      else:
        pushChild(a)

    of nnkIdent:
      attrSetters.add(newCall(ident"mountAttr", node, newLit($a), newLit("true")))

    else:
      pushChild(a)

  let createExpr: NimNode =
    if tagName == "fragment":
      newCall(ident"jsCreateFragment")
    else:
      newCall(ident"jsCreateElement", newCall(ident"cstring", newLit(tagName)))

  statements.add(newLetStmt(node, createExpr))

  for s in attrSetters:
    statements.add(s)

  for child in children:
    statements.add(lowerMountChildren(node, child))

  for i in 0 ..< eventNames.len:
    let eventNameExpr: NimNode = newCall(ident"cstring", newLit(eventNames[i]))
    let eventTypeSym: NimNode = genSym(nskLet, "eventType")
    let handlerSym: NimNode = genSym(nskLet, "handler")

    statements.add(newLetStmt(eventTypeSym, eventNameExpr))
    statements.add(newLetStmt(handlerSym, eventHandlers[i]))
    statements.add(newCall(ident"jsAddEventListener", node, eventTypeSym, handlerSym))
    statements.add(newCall(
      ident"registerCleanup",
      node,
      newProc(body = newCall(ident"jsRemoveEventListener", node, eventTypeSym, handlerSym))
    ))

  statements.add(node)

  result = statements


macro defineHtmlElements*(names: varargs[untyped]): untyped =
  result = newStmtList()

  proc tagNameOf(n: NimNode): string {.compileTime.} =
    case n.kind
    of nnkAccQuoted: $n[0]
    of nnkIdent: $n
    else: astToStr(n)

  for n in names:
    let s: string = tagNameOf(n)

    result.add(quote do:
      macro `n`*(args: varargs[untyped]): untyped =
        var call: NimNode = newCall(ident"defineHtmlElement")
        call.add(newLit(`s`))

        for it in args:
          call.add(it)
        result = call
    )


defineHtmlElements a,
  abbr,
  address,
  area,
  article,
  aside,
  audio,
  b,
  base,
  bdi,
  bdo,
  blockquote,
  body,
  br,
  button,
  canvas,
  caption,
  cite,
  code,
  col,
  colgroup,
  data,
  datalist,
  dd,
  del,
  details,
  dfn,
  dialog,
  d,  # div
  dl,
  dt,
  em,
  embed,
  fieldset,
  figcaption,
  figure,
  footer,
  form,
  fragment,
  h1,
  h2,
  h3,
  h4,
  h5,
  h6,
  head,
  header,
  hr,
  html,
  i,
  iframe,
  img,
  input,
  ins,
  kbd,
  label,
  legend,
  li,
  link,
  main,
  map,
  mark,
  menu,
  meta,
  meter,
  nav,
  noscript,
  obj,  # object
  ol,
  optgroup,
  option,
  output,
  p,
  param,
  picture,
  pre,
  progress,
  q,
  rp,
  rt,
  ruby,
  s,
  samp,
  script,
  section,
  select,
  slot,
  small,
  source,
  span,
  strong,
  style,
  sub,
  summary,
  sup,
  svg,
  table,
  tbody,
  td,
  tmpl,  # template
  textarea,
  tfoot,
  th,
  thead,
  time,
  title,
  tr,
  track,
  u,
  ul,
  v,  # var
  video,
  wbr

export
  dom,
  macros

export
  mount,
  overloads,
  routing,
  shims,
  signals,
  styled,
  types
